	.TITLE test
	.PSECT PMAIN
	; бит CLK (часы для плеера)
	CLKMSK=1
	; бит IN (входные данные для плеера)
	INMSK=2
	; бит OUT (выходные данные плеера)
	OUTMSK=1
	; Начальная задержка в 1 секунду
	STARTDL=23310.
.=.+100000
	jmp main
	nop
	rts pc
	; --------
	; Задержка
	; r0 - сколько раз по 42.9 микросекунд ждать
	; портит: r0
delay:
	mov r0,@#177706
	mov #177712,r0
	mov #24,(r0)	; 24 - запуск таймера + режим установки бита 7 при переходе через ноль
0wait:
	tstb (r0)
	bpl 0wait		; если бит 7 не установлен, то байт положителный
	rts pc

	; --------
	; Задержка при посылке одного бита
	; определяет скорость обмена
	; должна сохранять Z
	; портит: -
xfcdly:	
	nop
	nop
	nop
	nop
	rts pc

	; --------
	; Посылка/передача одного бита
	; Флаг C бит для передачи и принятый бит
	; портит: -
bitrw:
	bcs 1clk
	mov #0,@#177714			; сначала бит для передачи = 0 и часы = 0
	mov #CLKMSK,@#177714	; теперь  бит для передачи = 0 и часы = 1 (теперь плеер может считывать бит)
	bit #OUTMSK,@#177714    ; считывает что передал плеер
	jsr pc,xfcdly			; держим часы 1
	mov #0,@#177714			; держим бит для передачи = 0, но часы = 0
	br 1check
1clk:
	mov #INMSK,@#177714			; сначала бит для передачи = 1 и часы = 0
	mov #INMSK+CLKMSK,@#177714	; теперь  бит для передачи = 1 и часы = 1 (теперь плеер может считывать бит)
	bit #OUTMSK,@#177714		; считывает что передал плеер
	jsr pc,xfcdly				; держим часы 1
	mov #INMSK,@#177714			; держим бит для передачи = 1, но часы = 0
1check:
	jsr pc,xfcdly				; держим часы 0
	jsr pc,xfcdly				; держим часы 0
	clc							; перетаскиваем считанный бит от плеера из Z в C
	bne 1exit
	sec
1exit:	
	rts pc

	; --------
	; Послать один байт
	; r0 - байт
	; портит: r0, r1
sendb:
	mov #8.,r1
0loop:
	ror r0
	jsr pc,bitrw
	sob r1, 0loop
	rts pc

	; --------
	; Принять один байт
	; r0 - байт
	; портит: r0, r1
recvb:
	clr r0
	mov #8.,r1
1loop:
	clc						; приём байт происходи подачей команды CMD_NOP плееру
	jsr pc,bitrw
	ror r0
	sob r1, 1loop
	rts pc

	; ===================================================================
main:
	; стек
	mov #1000,sp
	; Начальные значения пинов
	mov #0, @#177714
	; Небольшая задержка чтобы плеер успел приготовиться
	; даже если таймер сработает на второй раз это не имеет значения.
	mov #STARTDL,r0
	jsr pc,delay
	nop
	nop

	; ============
	; тестовая часть
	; чистим экран
	;mov #40000,r0
	;mov #20000,r1
;2loop:
	;clr (r0)+
	;sob r1, 2loop

	; послать CMD_RESET (1)
	mov #1,r0
	jsr pc, sendb
	; прочитать ответ
	jsr pc,recvb

	br .
	.END

